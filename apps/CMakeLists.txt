if(WIN32)
    set(OSVR_SERVER_RESOURCE osvr_server.rc)
else()
    set(OSVR_SERVER_RESOURCE)
endif()

add_executable(osvr_server
    osvr_server.cpp
    ${OSVR_SERVER_RESOURCE})
add_executable(BasicServer BasicServer.cpp)
add_executable(osvr_calibrate
    osvr_calibrate.cpp
    ClientMainloop.h
    RecomposeTransform.h
    WrapRoute.h)

foreach(server BasicServer osvr_server osvr_calibrate)
    target_link_libraries(${server} osvrServer)
    set_target_properties(${server} PROPERTIES
        FOLDER "OSVR Stock Applications")
endforeach()

target_link_libraries(osvr_server ${JSONCPP_LIBRARY})
target_link_libraries(osvr_calibrate ${JSONCPP_LIBRARY} osvrClientKitCpp osvrCommon eigen-headers boost_thread ${Boost_PROGRAM_OPTIONS_LIBRARIES})

add_executable(osvr_reset_yaw
    osvr_reset_yaw.cpp
    ClientMainloop.h
    ClientMainloopThread.h
    RecomposeTransform.h
    WrapRoute.h)
target_link_libraries(osvr_reset_yaw ${JSONCPP_LIBRARY} osvrClientKitCpp osvrClient osvrCommon eigen-headers boost_thread ${Boost_PROGRAM_OPTIONS_LIBRARIES})
set_target_properties(osvr_reset_yaw PROPERTIES
    FOLDER "OSVR Stock Applications")

add_executable(diagnose-rotation
    diagnose-rotation.cpp
    ClientMainloop.h
    ClientMainloopThread.h
    RecomposeTransform.h)
target_link_libraries(diagnose-rotation ${JSONCPP_LIBRARY} osvrClientKitCpp osvrClient osvrCommon eigen-headers boost_thread ${Boost_PROGRAM_OPTIONS_LIBRARIES})
set_target_properties(diagnose-rotation PROPERTIES
    FOLDER "OSVR Stock Applications")

install(TARGETS osvr_server osvr_reset_yaw
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR} COMPONENT Runtime)
install(TARGETS BasicServer osvr_calibrate
    RUNTIME DESTINATION ${EXTRA_SAMPLE_BINDIR} COMPONENT ExtraSampleBinaries)

# Grab all the config files with a glob, to avoid missing one.
file(GLOB CONFIG_FILES RELATIVE "${CMAKE_CURRENT_SOURCE_DIR}" "${CMAKE_CURRENT_SOURCE_DIR}/osvr_server_config*.json")

foreach(FN ${CONFIG_FILES})
    add_custom_command(TARGET osvr_server POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy "${CMAKE_CURRENT_SOURCE_DIR}/${FN}" $<TARGET_FILE_DIR:osvr_server>
        COMMENT "Copying JSON config file sample ${FN}"
        VERBATIM)
    install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/${FN}"
        DESTINATION ${CMAKE_INSTALL_BINDIR} COMPONENT Runtime)
endforeach()
